dependencies {
    implementation deps.log
    implementation deps.tsconfig
    implementation platform(deps.aws)
    implementation deps.awsSecrets

    testImplementation deps.junitApi
    testImplementation deps.mockito
    testImplementation deps.joor
    testRuntimeOnly deps.junitRuntime
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            // read publishing properties (from gradle.properties) in a way that the build doesnt break if missing
            def nexusUser = project.hasProperty('nexusUser') ? project.getProperty('nexusUser') : 'nexusUser'
            def nexusPassword = project.hasProperty('nexusPassword') ? project.getProperty('nexusPassword') : 'nexusPassword'

            name = 'nexus'
            url = getRepositoryUrl()

            credentials {
                username nexusUser
                password nexusPassword
            }
        }
    }
}

def getRepositoryUrl() {
    def hostProp = 'nexusHost'
    def nexusHost = project.hasProperty(hostProp) ? project.getProperty(hostProp) : 'localhost'
    def releasesRepoUrl  = "http://$nexusHost:8081/nexus/content/repositories/releases"
    def snapshotsRepoUrl = "http://$nexusHost:8081/nexus/content/repositories/snapshots"
    return version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl;
}
